plugins {
    id 'java'
    id 'java-library'
    id 'application'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.omaraloraini'
version '1.2-SNAPSHOT'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'testcontainers-hdfs'
                description = 'Docker container image for Hadoop HDFS mini cluster, and a testcontainers libray API for using it'
                url = 'https://github.com/omaraloraini/testcontainers-hdfs'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'omaraloraini'
                        name = 'Omar Aloraini'
                        email = 'aloraini.omar@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com:omaraloraini/testcontainers-hdfs.git'
                    developerConnection = 'scm:git:ssh://github.com:omaraloraini/testcontainers-hdfs.git'
                    url = 'https://github.com/omaraloraini/testcontainers-hdfs/tree/main'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'snapshot'
            url = "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

application {
    mainClass = 'omaraloraini.testcontainers.hdfs.Main'
}

dependencies {
    // https://mvnrepository.com/artifact/org.apache.hadoop/hadoop-minicluster
    implementation group: 'org.apache.hadoop', name: 'hadoop-minicluster', version: '3.0.3'
    // https://mvnrepository.com/artifact/org.testcontainers/testcontainers
    implementation group: 'org.testcontainers', name: 'testcontainers', version: '1.15.1'
}
